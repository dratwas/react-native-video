apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        //noinspection GradleDependency
        classpath rootProject.ext.has('gradleBuildTools') ? rootProject.ext.get('gradleBuildTools') : 'com.android.tools.build:gradle:3.3.1'
    }
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 28)
    buildToolsVersion safeExtGet('buildToolsVersion', '28.0.3')
 
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 28)
        versionCode 1
        versionName "1.0"
	ndk {
            abiFilters "armeabi-v7a", "x86"
        }

    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
    maven {
        url "https://dl.bintray.com/wordpress-mobile/react-native-mirror/"
    }
}

dependencies {

    if (project == rootProject) {
        // If this is the root project (e.g. Jitpack), specify a version
        implementation 'com.facebook.react:react-native:0.60.0-rc.1'
    } else {
        //noinspection GradleDynamicVersion
        compileOnly "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    }

    implementation('com.google.android.exoplayer:exoplayer:2.10.1') {
        exclude group: 'com.android.support'
    }

    // All support libs must use the same version
    implementation "androidx.annotation:annotation:1.0.0"
    implementation "androidx.core:core:1.0.2"
    implementation "androidx.media:media:1.0.0"

    implementation('com.google.android.exoplayer:extension-okhttp:2.9.3') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

}
